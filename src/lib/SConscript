# coding=utf-8

#
# Author: Tomi Jylh√§-Ollila, Finland 2010-2013
#
# This file is part of Kunquat.
#
# CC0 1.0 Universal, http://creativecommons.org/publicdomain/zero/1.0/
#
# To the extent possible under law, Kunquat Affirmers have waived all
# copyright and related or neighboring rights to Kunquat.
#

import glob
import os.path


Import('env')

Export('env')

if env['enable_tests']:
    SConscript('test/SConscript')

env_lib = env.Clone()

if not env_lib.GetOption('clean') and 'check' in env['LIBS']:
    env_lib['LIBS'].remove('check')

#print 'lib: ' + env_lib.Dump('LIBS')

env_lib.Append(CPPPATH = ['#/src/lib',
                          '#/src/lib/events',
                          '#/src/lib/generators',
                          '#/src/lib/dsps',
                          '#/src/include'])


env_lib.sources = glob.glob('*.c')

Export('env_lib')

SConscript('events/SConscript')
SConscript('generators/SConscript')
SConscript('dsps/SConscript')
SConscript('player/SConscript')


env_lib['lib_version_major'] = 0

ln_cmd = 'ln -s ${SOURCE.file} ${TARGET.file}' # TARGET is the link

def InstallLibCreatePath(env, path, source):
    target = env.InstallCreatePath(path, source)
    if 'install' in COMMAND_LINE_TARGETS and not env_lib['enable_profiling']:
        for file in source:
            env.Command(os.path.join(path, '%s.%s' % (str(file),
                                           str(env['lib_version_major']))),
                        os.path.join(path, str(file)), ln_cmd, chdir=1)
#            env.fs.Entry(
    return target

env_lib.AddMethod(InstallLibCreatePath, 'InstallLibCreatePath')


if env_lib['enable_profiling']:
    libkunquat = env_lib.StaticLibrary('kunquat', env_lib.sources)
else:
    env_lib.Append(SHLINKFLAGS = ['-Wl,-soname,libkunquat.so.' +
                                  str(env_lib['lib_version_major'])])
    libkunquat = env_lib.SharedLibrary('kunquat', env_lib.sources)

# For some reason, SCons sets the mode to 0755 by default
env_lib.AddPostAction(libkunquat, Chmod(libkunquat[0].abspath, 0644))
env_lib.InstallLibCreatePath(env_lib['prefix'] + '/lib', libkunquat)

# This hack fixes the test builds.
if not env_lib['enable_profiling']:
    env_lib.Command('libkunquat.so.%d' % env_lib['lib_version_major'],
                    'libkunquat.so', ln_cmd, chdir=1)


