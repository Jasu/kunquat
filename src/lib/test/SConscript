# coding=utf-8


# Author: Tomi Jylh√§-Ollila, Finland 2010-2012
#
# This file is part of Kunquat.
#
# CC0 1.0 Universal, http://creativecommons.org/publicdomain/zero/1.0/
#
# To the extent possible under law, Kunquat Affirmers have waived all
# copyright and related or neighboring rights to Kunquat.


import glob


Import('env')

Export('env')


env_test = env.Clone()

env_test.Append(CPPPATH = ['.',
                           '#/src/lib',
                           '#/src/lib/events',
                           '#/src/lib/generators',
                           '#/src/include'])
env_test.Append(LIBPATH = ['#/src/lib'])
env_test.Append(LIBS = ['kunquat', 'check'])

if env_test['enable_tests_mem_debug']:
    env_test.Append(CCFLAGS = ['-DK_MEM_DEBUG'])


programs = []
for source in glob.glob('*.c'):
    exe_name = source[:-2]
    programs.append(env_test.Program(exe_name, [source]))


if not env.GetOption('clean'):
    run_prefix = 'env LD_LIBRARY_PATH=src/lib '
    if env_test['enable_tests_mem_debug']:
        run_prefix += 'src/lib/test/mem_debug_run.py '

    for program in programs:
        env_test.AddPostAction(program, run_prefix + program[0].abspath)


