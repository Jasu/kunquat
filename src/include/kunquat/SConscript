# coding=utf-8

#
# Author: Tomi Jylh√§-Ollila, Finland 2010-2012
#
# This file is part of Kunquat.
#
# CC0 1.0 Universal, http://creativecommons.org/publicdomain/zero/1.0/
#
# To the extent possible under law, Kunquat Affirmers have waived all
# copyright and related or neighboring rights to Kunquat.
#

import glob
import os.path


Import('env')

env_head = env.Clone()

include_path = env_head['prefix'] + '/include/kunquat'
files = glob.glob('*.h')
ih = env_head.InstallCreatePath(include_path, files)
env_head.Clean(ih, include_path)


ln_cmd = 'ln -s ${SOURCE.file} ${TARGET.file}' # TARGET is the link

def InstallManCreatePath(env, path, source):
    target = env.InstallCreatePath(path, source)
    if 'install' in COMMAND_LINE_TARGETS:
        links = { 'kunquat-handle.3' : [
                        'kqt_new_Handle_r.3',
                        'kqt_Handle_get_data.3',
                        'kqt_Handle_get_data_length.3',
                        'kqt_Handle_free_data.3',
                        'kqt_Handle_set_data.3',
                        'kqt_Handle_get_error.3',
                        'kqt_Handle_clear_error.3',
                        'kqt_del_Handle.3'],

                  'kunquat-player-interface.3' : [
                        'kqt_Handle_mix.3',
                        'kqt_Handle_get_buffer.3',
                        'kqt_Handle_set_mixing_rate.3',
                        'kqt_Handle_get_mixing_rate.3',
                        'kqt_Handle_set_buffer_size.3',
                        'kqt_Handle_get_buffer_size.3',
                        'kqt_Handle_get_duration.3',
                        'kqt_Handle_set_position.3',
                        'kqt_Handle_get_position.3',
                        'kqt_Handle_fire.3'],
                }
        for fname in (f for f in source if f in links):
            for link in links[fname]:
                env.Command(os.path.join(path, link),
                            os.path.join(path, fname), ln_cmd, chdir=1)

env_head.AddMethod(InstallManCreatePath)

man_path = env_head['prefix'] + '/share/man/man3'
man_pages = glob.glob('kunquat-*.3')
env_head.InstallManCreatePath(man_path, man_pages)


